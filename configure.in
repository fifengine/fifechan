AC_PREREQ(2.57)

AC_INIT(guichan, 0.2.0, jansem@darkbits.org, Guichan)

AC_CANONICAL_SYSTEM
AC_SUBST(target)

ALLEGRO="disabled"
OPENGL="disabled"
SDL="disabled"
SDLIMAGE="disabled"
X="disabled"

GUICHAN_MAJOR_VERSION=0
GUICHAN_MINOR_VERSION=2
GUICHAN_MICRO_VERSION=0
GUICHAN_INTERFACE_AGE=0
GUICHAN_BINARY_AGE=0
GUICHAN_VERSION=$GUICHAN_MAJOR_VERSION.$GUICHAN_MINOR_VERSION.$GUICHAN_MICRO_VERSION

AC_SUBST(GUICHAN_MAJOR_VERSION)
AC_SUBST(GUICHAN_MINOR_VERSION)
AC_SUBST(GUICHAN_MICRO_VERSION)
AC_SUBST(GUICHAN_VERSION)
AC_SUBST(GUICHAN_INTERFACE_AGE)
AC_SUBST(GUICHAN_BINARY_AGE)
AC_SUBST(GUICHAN_VERSION)

LT_RELEASE=$GUICHAN_MAJOR_VERSION.$GUICHAN_MINOR_VERSION
LT_CURRENT=`expr $GUICHAN_MICRO_VERSION - $GUICHAN_INTERFACE_AGE`
LT_REVISION=$GUICHAN_INTERFACE_AGE
LT_AGE=`expr $GUICHAN_BINARY_AGE - $GUICHAN_INTERFACE_AGE`

AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

AM_INIT_AUTOMAKE(Guichan, $GUICHAN_VERSION)
AM_CONFIG_HEADER([include/config.hpp])

dnl AC_CONFIG_SRCDIR([src/])

AC_PROG_CXX
AC_PROG_CC

AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
AC_PROG_MAKE_SET
AC_LANG_CPLUSPLUS
AC_PROG_INSTALL
AC_HEADER_STDC

CXXFLAGS="$CXXFLAGS -pedantic -Wall -Werror -Wno-unused -DGUICHAN_BUILD"

NoX()
{
  AC_MSG_WARN(Xlib support skipped when Xlib not found.)
  X="no"
}

HaveX()
{

  X="yes"
  X_LIBS="$X_LIBS -L$x_libraries -lX11"
  GUICHAN_EXTRADIRS="$GUICHAN_EXTRADIRS x"
}

NoOpenGL()
{
  AC_MSG_WARN(OpenGL support skipped when OpenGL not found.)
  OPENGL="no"
}

HaveOpenGL()
{
  OPENGL="yes"
  GUICHAN_EXTRADIRS="$GUICHAN_EXTRADIRS opengl"
  OPENGL_LIBS="$OPENGL_LIBS -lGL"
}

ForceOpenGL()
{
  AC_MSG_WARN(OpenGL support forced.)
  OPENGL="forced"
  GUICHAN_EXTRADIRS="$GUICHAN_EXTRADIRS opengl"
  OPENGL_LIBS="$OPENGL_LIBS -lGL"
}

NoSDLImage()
{
  AC_MSG_WARN(SDL_image support skipped when SDL_image not found.)
  SDLIMAGE="no"
}

HaveSDLImage()
{
  SDLIMAGE="yes"
  SDL_LIBS="$SDL_LIBS -lSDL_image"
}

NoSDL()
{
  AC_MSG_WARN(SDL support skipped when SDL not found.)
}

ForceSDLImage()
{
  AC_MSG_WARN(SDL Image support forced.)
  SDLIMAGE="forced"
  SDL_LIBS="$SDL_LIBS -lSDL_image"
}

HaveSDL()
{
  SDL="yes"
  GUICHAN_EXTRADIRS="$GUICHAN_EXTRADIRS sdl"
}

ForceSDL()
{
  AC_MSG_WARN(SDL support forced.)
  SDL="forced"
  GUICHAN_EXTRADIRS="$GUICHAN_EXTRADIRS sdl"
}

NoAllegro()
{
  AC_MSG_WARN(Allegro support skipped when Allegro not found.)    
}

HaveAllegro()
{
  ALLEGRO="yes"
  GUICHAN_EXTRADIRS="$GUICHAN_EXTRADIRS allegro"
}

ForceAllegro()
{
  AC_MSG_WARN(Allegro support forced.)
  ALLEGRO="forced"
  GUICHAN_EXTRADIRS="$GUICHAN_EXTRADIRS allegro"
}

AC_ARG_ENABLE(x,	     
[  --enable-x	Enable Xlib support [default=no]], 
               , enable_x=no)
if test x$enable_x = xyes; then
  AC_PATH_X
  if test "x$no_x" = "xyes"; then
    NoX
  else
  dnl   AC_CHECK_HEADERS([X11/Xlib.h])
  dnl   AC_CHECK_LIB([X11], [XOpenDisplay], HaveX, NoX)
    HaveX
  fi
fi

AC_ARG_ENABLE(opengl,	     
[  --enable-opengl	Enable OpenGL support [default=yes]], 
               , enable_opengl=yes)
AC_ARG_ENABLE(force_opengl,	  
[  --enable-force-opengl     Force OpenGL support (skip checks for OpenGL) [default=no]],
              , enable_force_opengl=no)
if test x$enable_opengl = xyes; then
  if test x$enable_force_opengl = xno; then
    AC_CHECK_HEADERS(GL/gl.h)
    AC_CHECK_LIB([GL], [glBegin], HaveOpenGL, NoOpenGL)
  else
    ForceOpenGL
  fi
fi

AC_ARG_ENABLE(sdlimage,	     
[  --enable-sdlimage	Enable SDL_image support [default=yes]], 
              , enable_sdlimage=yes)
AC_ARG_ENABLE(force_sdlimage,	  
[  --enable-force-sdlimage     Force SDL Image support (skip checks for SDL Image) [default=no]],
              , enable_force_sdlimage=no)
if test x$enable_sdlimage = xyes; then
  if test x$enable_force_sdlimage = xno; then
    AC_CHECK_HEADERS([SDL/SDL_image.h])
    AC_CHECK_LIB([SDL_image], [IMG_Load], HaveSDLImage, NoSDLImage)
  else
    ForceSDLImage
  fi
fi

AC_ARG_ENABLE(allegro,	  
[  --enable-allegro	  Enable Allegro support [default=yes]],
              , enable_allegro=yes)
AC_ARG_ENABLE(force_allegro,	  
[  --enable-force-allegro     Force Allegro support (skip checks for Allegro) [default=no]],
              , enable_force_allegro=no)
if test x$enable_allegro = xyes; then
  if test x$enable_force_allegro = xno; then
    AC_CHECK_HEADERS([allegro.h])
    AC_CHECK_PROG(HAVE_ALLEGRO, allegro-config, yes)
    if test "x$HAVE_ALLEGRO" != "xyes"; then
      NoAllegro  
    else
      HaveAllegro
    fi
  else
    ForceAllegro
  fi
fi

AC_ARG_ENABLE(sdl,	  
[  --enable-sdl	  Enable SDL support [default=yes]],
             , enable_sdl=yes)
AC_ARG_ENABLE(force_sdl,	  
[  --enable-force-sdl     Force SDL support (skip checks for SDL) [default=no]],
              , enable_force_sdl=no)
if test x$enable_sdl = xyes; then
  if test x$enable_force_sdl = xno; then
    AC_CHECK_HEADERS(SDL/SDL.h)
    AC_CHECK_PROG(HAVE_SDL, sdl-config, yes)
    if test "x$HAVE_SDL" != "xyes"; then
      NoSDL
    else
      HaveSDL
    fi
  else
    ForceSDL
  fi
fi

AC_SUBST([OPENGL_LIBS])
AC_SUBST([SDL_LIBS])
AC_SUBST([X_LIBS])
AC_SUBST([GUICHAN_EXTRADIRS])
AC_SUBST([GUICHAN_EXTRALIBS])

AC_OUTPUT([
Makefile
include/Makefile
include/guichan/Makefile
include/guichan/allegro/Makefile
include/guichan/opengl/Makefile
include/guichan/sdl/Makefile
include/guichan/widgets/Makefile
include/guichan/x/Makefile
src/Makefile
src/allegro/Makefile
src/opengl/Makefile
src/sdl/Makefile
src/widgets/Makefile
src/x/Makefile])
echo ""
echo "IMPORTANT! You are about install a BETA version of Guichan"
echo "which most likely will lose binary compatibility with the"
echo "future stable release. This release should only be used" 
echo "for testing."
echo ""
echo "-------------------------------"
echo "Guichan ready for compilation!" 
echo "-------------------------------"
echo "* Allegro   = "$ALLEGRO
echo "* OpenGL    = "$OPENGL
echo "* SDL       = "$SDL
echo "* SDL Image = "$SDLIMAGE
echo "--------------------------------"
